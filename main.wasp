app askTheDocuments {
  wasp: {
    version: "^0.18.0"
  },
  title: "PG Vector Example",
  auth: {
    userEntity: User,
    methods: {
      google: {
        userSignupFields: import { fields } from "@src/auth/google",
        configFn: import { getConfig } from "@src/auth/google",
      }
    },
    onAuthFailedRedirectTo: "/"
  },
  client: {
    rootComponent: import { Layout } from "@src/Layout",
  },
  server: {
    envValidationSchema: import { serverEnvValidation } from "@src/env"
  },
}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import { Main } from "@src/pages/MainPage"
}

action embedDocument {
  fn: import { embedDocument } from "@src/documents",
  entities: [Document],
}

action getScrapeCandidates {
  fn: import { getScrapeCandidates } from "@src/documents",
  entities: [Document],
}

query getDocuments {
  fn: import { getDocuments } from "@src/documents",
  entities: [Document]
}

action searchDocuments {
  fn: import { searchDocuments } from "@src/documents",
  entities: [Document]
}

action askDocuments {
  fn: import { askDocuments } from "@src/documents",
  entities: [Document]
}

action deleteDocument {
  fn: import { deleteDocument } from "@src/documents",
  entities: [Document]
}

action deleteAllDocuments {
  fn: import { deleteAllDocuments } from "@src/documents", 
  entities: [Document]
}
