app todoApp {
  wasp: {
    version: "^0.18.0"
  },
  title: "ToDo App",
  // head: [],
  webSocket: {
    fn: import { webSocketFn } from "@src/features/chat/webSocket",
    // autoConnect: false
  },
  auth: {
    userEntity: User,
    methods: {
      // usernameAndPassword: {
      //  userSignupFields: import { userSignupFields } from "@src/features/auth/github",
      // },
      slack: {
        configFn: import { config } from "@src/features/auth/providers/slack",
        userSignupFields: import { userSignupFields } from "@src/features/auth/providers/slack"
      },
      discord: {
        configFn: import { config } from "@src/features/auth/providers/discord",
        userSignupFields: import { userSignupFields } from "@src/features/auth/providers/discord"
      },
      google: {
        configFn: import { config } from "@src/features/auth/providers/google",
        userSignupFields: import { userSignupFields } from "@src/features/auth/providers/google"
      },
      gitHub: {
        configFn: import { config } from "@src/features/auth/providers/github.js",
        userSignupFields: import { userSignupFields } from "@src/features/auth/providers/github.js"
      },
      // keycloak: {},
      email: {
        userSignupFields: import { userSignupFields } from "@src/features/auth/providers/email",
        fromField: {
          name: "ToDO App",
          email: "todoapp@mg.wasp.sh"
        },
        emailVerification: {
          getEmailContentFn: import { getVerificationEmailContent } from "@src/features/auth/providers/email",
          clientRoute: EmailVerificationRoute,
        },
        passwordReset: {
          getEmailContentFn: import { getPasswordResetEmailContent } from "@src/features/auth/providers/email",
          clientRoute: PasswordResetRoute
        },
      },
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/",
    onBeforeSignup: import { onBeforeSignup } from "@src/features/auth/hooks.js",
    onAfterSignup: import { onAfterSignup } from "@src/features/auth/hooks.js",
    onAfterEmailVerified: import { onAfterEmailVerified } from "@src/features/auth/hooks.js",
    onBeforeLogin: import { onBeforeLogin } from "@src/features/auth/hooks.js",
    onAfterLogin: import { onAfterLogin } from "@src/features/auth/hooks.js",
  },
  server: {
    setupFn: import { setup } from "@src/serverSetup",
    middlewareConfigFn: import { serverMiddlewareFn } from "@src/serverSetup",
    envValidationSchema: import { serverEnvValidationSchema } from "@src/env",
  },
  client: {
    rootComponent: import { App } from "@src/App",
    setupFn: import { setup } from "@src/clientSetup",
    envValidationSchema: import { clientEnvValidationSchema } from "@src/env",
  },
  db: {
    seeds: [
      import { devSeedSimple } from "@src/features/db/seeds",
      import { prodSeed } from "@src/features/db/seeds"
    ],
    prismaSetupFn: import { setUpPrisma } from "@src/features/db/prisma",
  },
  emailSender: {
    provider: SMTP,
    defaultFrom: {
      email: "todoapp@mg.wasp.sh"
    },
  },
}

// --------- General --------- //

route HomeRoute { path: "/", to: HomePage }
page HomePage {
  component: import { HomePage } from "@src/pages/HomePage"
}

route CatchAllRoute { path: "*", to: CatchAllPage }
page CatchAllPage {
  component: import { CatchAllPage } from "@src/pages/CatchAllPage"
}

// --------- Auth --------- //

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@src/features/auth/pages/Signup"
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/features/auth/pages/Login"
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordReset } from "@src/features/auth/pages/PasswordReset",
}

route EmailVerificationRoute { path: "/email-verification-", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerification } from "@src/features/auth/pages/EmailVerification",
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordReset } from "@src/features/auth/pages/RequestPasswordReset",
}

route ProfileRoute { path: "/profile", to: ProfilePage }
page ProfilePage {
  authRequired: true,
  component: import { ProfilePage } from "@src/features/auth/pages/ProfilePage"
}

// --------- Manual Signup --------- //

route ManualSignupRoute { path: "/manual-signup", to: ManualSignupPage }
page ManualSignupPage {
  component: import { ManualSignupPage } from "@src/features/auth/pages/ManualSignupPage"
}

// --------- Custom Signup --------- //

route CustomSignupRoute { path: "/custom-signup", to: CustomSignupPage }
page CustomSignupPage {
  component: import { CustomSignupPage } from "@src/features/auth/pages/CustomSignupPage"
}

action customSignup {
  fn: import { signup } from "@src/features/auth/customSignup",
}


// --------- Operations --------- //

route TasksRoute { path: "/tasks", to: TasksPage }
page TasksPage {
  authRequired: true,
  component: import { TasksPage } from "@src/features/operations/pages/TasksPage"
}

route TaskRoute { path: "/tasks/:id", to: TaskPage }
page TaskPage {
  authRequired: true,
  component: import { TaskDetailPage } from "@src/features/operations/pages/TaskDetailPage"
}

query getTasks {
  fn: import { getTasks } from "@src/features/operations/queries",
  entities: [Task]
}

query getNumTasks {
  fn: import { getNumTasks } from "@src/features/operations/queries",
  entities: [Task],
  auth: false
}

query getTask {
  fn: import { getTask } from "@src/features/operations/queries",
  entities: [Task]
}

action createTask {
  fn: import { createTask } from "@src/features/operations/actions",
  entities: [Task]
}

action updateTaskIsDone {
  fn: import { updateTaskIsDone } from "@src/features/operations/actions",
  entities: [Task]
}

action deleteCompletedTasks {
  fn: import { deleteCompletedTasks } from "@src/features/operations/actions",
  entities: [Task]
}

action toggleAllTasks {
  fn: import { toggleAllTasks } from "@src/features/operations/actions",
  entities: [Task]
}

query getSerializedObjects {
  fn: import { getSerializedObjects } from "@src/features/operations/queries",
}

route SerializationRoute { path: "/serialization", to: SerializationPage }
page SerializationPage {
  component: import { SerializationPage } from "@src/features/operations/pages/SerializationPage",
}

// --------- Jobs --------- //

route JobsRoute { path: "/jobs", to: JobsPage }
page JobsPage {
  authRequired: true,
  component: import { JobsPage } from "@src/features/jobs/pages/JobsPage"
}

action requestUppercaseText {
  fn: import { requestUppercaseText } from "@src/features/jobs/uppercaseText",
  entities: [UppercaseTextRequest],
}

query getTextUppercaseRequests {
  fn: import { getTextUppercaseRequests } from "@src/features/jobs/uppercaseText",
  entities: [UppercaseTextRequest],
}

job uppercaseTextJob {
  executor: PgBoss,
  perform: {
    fn: import { uppercaseText } from "@src/features/jobs/uppercaseText",
  },
  entities: [UppercaseTextRequest]
}

job mySpecialJob {
  executor: PgBoss,
  perform: {
    fn: import { foo } from "@src/features/jobs/bar",
    executorOptions: {
      pgBoss: {=json { "retryLimit": 1 } json=}
    }
  },
  entities: [Task]
}

job mySpecialScheduledJob {
  executor: PgBoss,
  perform: {
    fn: import { foo } from "@src/features/jobs/bar"
  },
  schedule: {
    cron: "0 * * * *",
    args: {=json { "foo": "bar" } json=},
    executorOptions: {
      pgBoss: {=json { "retryLimit": 2 } json=}
    }
  }
}

// --------- APIs --------- //

route ApisRoute { path: "/apis", to: ApisPage }
page ApisPage {
  component: import { ApisPage } from "@src/features/apis/pages/ApisPage"
}

api fooBar {
  fn: import { fooBar } from "@src/features/apis/apis",
  middlewareConfigFn: import { fooBarMiddlewareFn } from "@src/features/apis/apis",
  entities: [Task],
  // ALL here let's our CORS work. If we did GET, we would need an apiNamespace over it with CORS.
  httpRoute: (ALL, "/foo/bar")
}

apiNamespace bar {
  middlewareConfigFn: import { barNamespaceMiddlewareFn } from "@src/features/apis/apis",
  path: "/bar"
}

api barBaz {
  fn: import { barBaz } from "@src/features/apis/apis",
  auth: false,
  entities: [Task],
  httpRoute: (GET, "/bar/baz")
}

api webhookCallback {
  fn: import { webhookCallback } from "@src/features/apis/apis",
  middlewareConfigFn: import { webhookCallbackMiddlewareFn } from "@src/features/apis/apis",
  httpRoute: (POST, "/webhook/callback"),
  auth: false
}

// --------- CRUD --------- //

crud tasks {
  entity: Task,
  operations: {
    get: {},
    getAll: {
      overrideFn: import { getAllTasks } from "@src/features/crud/crud",
    },
    create: {
      overrideFn: import { createTask } from "@src/features/crud/crud",
    },
    update: {},
    delete: {},
  },
}

route CrudListRoute { path: "/crud", to: CrudList }
page CrudList {
  component: import { ListPage } from "@src/features/crud/pages/ListPage",
  authRequired: true
}

route CrudDetailRoute { path: "/crud/:id", to: CrudDetail }
page CrudDetail {
  component: import { DetailPage } from "@src/features/crud/pages/DetailPage",
  authRequired: true
}

// --------- Streaming --------- //

api streamingText {
  httpRoute: (GET, "/api/streaming-test"),
  fn: import { getText } from "@src/features/streaming/api",
}

apiNamespace defaultMiddlewareForStreamingText {
  path: "/api/streaming-test",
  middlewareConfigFn: import { getMiddlewareConfig } from "@src/features/streaming/api",
}

route StreamingRoute { path: "/streaming", to: StreamingPage }
page StreamingPage {
  component: import { StreamingTestPage } from "@src/features/streaming/pages/StreamingTestPage",
}

// --------- Chat --------- //

route ChatRoute { path: "/chat", to: ChatPage }
page ChatPage {
  authRequired: true,
  component: import { ChatPage } from "@src/features/chat/pages/ChatPage"
}

// --------- Operations Type Testing --------- //

action testingAction {
  fn: import { testingAction } from "@src/rpcTests/operations/server",
  entities: []
}

query getDate {
  fn: import { getDate } from "@src/rpcTests/operations/definitions"
}

query getAnythingNoAuth {
  fn: import { getAnythingNoAuth } from "@src/rpcTests/operations/definitions",
  auth: false,
  entities: []
}

query getAnythingAuth {
  fn: import { getAnythingAuth } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: []
}

query getTrueVoid {
  fn: import { getTrueVoid } from "@src/rpcTests/operations/definitions",
  entities: []
}

query getAnyNoAuth {
  fn: import { getAnyNoAuth } from "@src/rpcTests/operations/definitions",
  auth: false,
  entities: []
}

query getAnyAuth {
  fn: import { getAnyAuth } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: []
}

query getAnyToNumberSpecified {
  fn: import { getAnyToNumberSpecified } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: []
}

action taskToTaskUnspecified {
  fn: import { taskToTaskUnspecified } from "@src/rpcTests/operations/definitions",
  entities: [Task]
}

action taskToTaskSatisfies {
  fn: import { taskToTaskSatisfies } from "@src/rpcTests/operations/definitions",
  entities: [Task]
}

action taskToTaskSpecified {
  fn: import { taskToTaskSpecified } from "@src/rpcTests/operations/definitions",
  entities: [Task]
}

action voidToStringAuth {
  fn: import { voidToStringAuth } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: [Task]
}

action voidToStringNoAuth {
  fn: import { voidToStringNoAuth } from "@src/rpcTests/operations/definitions",
  auth: false,
  entities: [Task]
}

action unspecifiedToNumber {
  fn: import { unspecifiedToNumber } from "@src/rpcTests/operations/definitions",
  entities: [Task]

}

action boolToStringAuth {
  fn: import { boolToStringAuth } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: [Task]
}

action boolToStringNoAuth {
  fn: import { boolToStringNoAuth } from "@src/rpcTests/operations/definitions",
  auth: false,
  entities: [Task]
}

action boolToVoidNoAuth {
  fn: import { boolToVoidNoAuth } from "@src/rpcTests/operations/definitions",
  auth: false,
  entities: [Task]
}

action boolToVoidAuth {
  fn: import { boolToVoidAuth } from "@src/rpcTests/operations/definitions",
  auth: true,
  entities: [Task]
}

action jsActionWithArgs {
  fn: import { jsActionWithArgs } from "@src/rpcTests/operations/jsDefinitions",
  auth: true,
  entities: [Task]
}
