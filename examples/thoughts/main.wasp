app Thoughts {
  wasp: {
    version: "^0.13.0"
  },
  title: "Thoughts",
  db: {
    system: PostgreSQL,
    seeds: [
      import { devSeedBasic } from "@src/server/seeds.js"
    ]
  },
  auth: {
    userEntity: User,
    methods: { usernameAndPassword: {} },
    onAuthFailedRedirectTo: "/login"
  },
}

route MainRoute { path: "/", to: MainPage }
page MainPage {
  component: import Main from "@src/client/MainPage.jsx",
  authRequired: true
}

route ThoughtsRoute { path: "/thoughts", to: ThoughtsPage }
page ThoughtsPage {
  component: import Thoughts from "@src/client/ThoughtsPage.jsx",
  authRequired: true
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/client/LoginPage.jsx"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@src/client/SignupPage.jsx"
}

action createThought {
  fn: import { createThought } from "@src/server/actions.js",
  entities: [Thought, Tag]
}

query getThoughts {
  fn: import { getThoughts } from "@src/server/queries.js",
  entities: [Thought]
}

query getTags {
  fn: import { getTags } from "@src/server/queries.js",
  entities: [Tag]
}

entity Thought {=psl
    id          Int       @id @default(autoincrement())
    textMarkdown          String
    createdAt   DateTime  @default(now())
    updatedAt   DateTime  @updatedAt
    tags        Tag[]

    user        User      @relation(fields: [userId], references: [id])
    userId      Int
psl=}

entity Tag {=psl
    id          Int       @id @default(autoincrement())
    name        String
    createdAt   DateTime  @default(now())
    thoughts    Thought[]

    user        User      @relation(fields: [userId], references: [id])
    userId      Int

    @@unique([name, userId])
psl=}

entity User {=psl
    id          Int       @id @default(autoincrement())

    thoughts    Thought[]
    tags        Tag[]
psl=}
